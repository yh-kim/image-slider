apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = "com.pickth"
version = propVersionName
// compile 'group:moduleName:version name' 의 형태로 사용됨

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            name 'image-slider'
            version version
            description = 'Is image slider'


            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id 'yh-kim'
                    name 'Yonghoon Kim'
                    email 'yonghoon.kim@pickth.com'
                }
            }
            scm {
                connection 'https://github.com/yh-kim/image-slider'
                developerConnection 'https://github.com/yh-kim/image-slider'
            }
        }
    }
}


if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

artifacts {
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "ImageSlider"
        name = "image-slider"
        vcsUrl = 'https://github.com/yh-kim/image-slider'
        licenses = ["Apache-2.0"]
        publish = true
        version {
            name = version
            desc = version
            vcsTag = version

            gpg {
                sign = true
            }
        }
    }
}